apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def buildCode() {
    Properties versionProps = new Properties()
    def versionPropsFile = file('version.properties')
    if (versionPropsFile.exists())
        versionProps.load(new FileInputStream(versionPropsFile))
    int code = new Integer((String) versionProps['VERSION_CODE'] ?: "0") + 1
    versionProps['VERSION_CODE'] = code.toString()
    versionProps.store(versionPropsFile.newWriter(), null)
    return code
}

android {
    signingConfigs {
        SorceryKey {
            keyAlias sorceryKeyAlias
            keyPassword sourcekKeyPassword
            storeFile file(sourceStoreFile)
            storePassword sourcesStorePassword
        }
    }
    compileSdkVersion 'android-O'
    buildToolsVersion '26.0.0 rc2'

    int code = buildCode()

    defaultConfig {
        applicationId "com.sorcerer.sorcery.iconpack"
        versionCode code
        versionName "4.4." + code
        minSdkVersion 21
        targetSdkVersion 25
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        debug {
            multiDexEnabled false
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.SorceryKey
        }
        release {
            multiDexEnabled false
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.SorceryKey
        }
    }
    buildTypes.each {
        it.buildConfigField 'String', 'LEANCLOUD_ID', leancloudId
        it.buildConfigField 'String', 'LEANCLOUD_KEY', leancloudKey
        it.buildConfigField 'String', 'HE_WEATHER_KEY', heWeatherKey
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    compile "com.android.support:appcompat-v7:$support_library_version"
    compile "com.android.support:design:$support_library_version"
    compile "com.android.support:support-v4:$support_library_version"
    compile "com.android.support:cardview-v7:$support_library_version"
    compile "com.android.support:recyclerview-v7:$support_library_version"
    compile "com.android.support:preference-v14:$support_library_version"
    compile "com.android.support:support-fragment:$support_library_version"
    compile "com.android.support:support-v13:$support_library_version"

    // material dialog
    compile 'com.afollestad.material-dialogs:core:0.9.4.5'
    compile 'com.afollestad.material-dialogs:commons:0.9.4.5'

    // material drawer
    compile('com.mikepenz:materialdrawer:5.9.2@aar') {
        transitive = true
    }

    // indicator
    compile 'com.wang.avi:library:2.1.3'

    // xposed
    provided 'de.robv.android.xposed:api:82'
    provided 'de.robv.android.xposed:api:82:sources'

    // butterknife
    compile 'com.jakewharton:butterknife:8.6.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.6.0'

    // glide
    compile 'com.github.bumptech.glide:glide:4.0.0-RC0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.0.0-RC0'

    // rx
    compile 'com.artemzin.rxjava:proguard-rules:1.2.10.0'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile "io.reactivex.rxjava2:rxjava:$rxjava2_version"
    compile 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'
    compile 'com.github.VictorAlbertos:RxActivityResult:0.4.4-2.x'
    compile 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    compile 'com.f2prateek.rx.preferences2:rx-preferences:2.0.0-RC1'
    compile 'com.squareup.sqlbrite:sqlbrite:1.1.1'

    // timber
    compile 'com.jakewharton.timber:timber:4.5.1'

    // iconics
    compile 'com.mikepenz:iconics-core:2.8.6@aar'
    compile 'com.mikepenz:google-material-typeface:3.0.1.0.original@aar'
    compile 'com.mikepenz:material-design-iconic-typeface:2.2.0.2@aar'
    compile 'com.mikepenz:community-material-typeface:1.8.36.1@aar'
    compile 'com.mikepenz:fontawesome-typeface:4.7.0.0@aar'

    // bugly
    compile 'com.tencent.bugly:crashreport:2.4.0'

    // slice
    compile 'com.github.mthli:Slice:v1.2'

    // gson
    compile 'com.google.code.gson:gson:2.8.1'

    // jsoup
    compile 'org.jsoup:jsoup:1.10.2'

    // stream on java7
    compile 'com.annimon:stream:1.1.7'

    // retrofit
    compile "com.squareup.retrofit2:retrofit:$retrofit_version"
    compile "com.squareup.retrofit2:converter-gson:$retrofit_version"
    compile "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"

    // autoValue
    provided 'com.google.auto.value:auto-value:1.3'
    annotationProcessor 'com.google.auto.value:auto-value:1.3'
    annotationProcessor 'com.ryanharter.auto.value:auto-value-parcel:0.2.5'

    // su
    compile 'eu.chainfire:libsuperuser:1.0.0.201704021214'

    // bottom sheet
    compile 'com.flipboard:bottomsheet-commons:1.5.3'

    // fastscroll
    compile 'com.github.turing-tech:MaterialScrollBar:12.3.1'

    // dagger
    compile 'com.google.dagger:dagger:2.11-rc2'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11-rc2'

    // markdownView
    compile 'com.github.tiagohm.MarkdownView:library:0.16.0'

    debugCompile 'com.amitshekhar.android:debug-db:1.0.0'
    compile 'cn.bingoogolapple:bga-swipebacklayout:1.0.8@aar'
    compile 'com.jaeger.statusbarutil:library:1.4.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    // For Lifecycles, LiveData, and ViewModel
    compile 'android.arch.lifecycle:runtime:1.0.0-alpha2'
    compile 'android.arch.lifecycle:extensions:1.0.0-alpha2'
    annotationProcessor "android.arch.lifecycle:compiler:1.0.0-alpha2"

    // For Room
    compile 'android.arch.persistence.room:runtime:1.0.0-alpha2'
    annotationProcessor "android.arch.persistence.room:compiler:1.0.0-alpha2"
}