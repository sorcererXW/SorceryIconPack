apply plugin: 'com.android.application'
apply plugin: 'img-optimizer'

def buildCode() {
    Properties versionProps = new Properties()
    def versionPropsFile = file('version.properties')
    if (versionPropsFile.exists())
        versionProps.load(new FileInputStream(versionPropsFile))
    int code = new Integer((String) versionProps['VERSION_CODE'] ?: "0") + 1
    versionProps['VERSION_CODE'] = code.toString()
    versionProps.store(versionPropsFile.newWriter(), null)
    return code
}

optimizerOptions {
    triggerSize 10 // kb
    type "lossy"
}

android {
    signingConfigs {
        SorceryKey {
            keyAlias sorceryKeyAlias
            keyPassword sourcekKeyPassword
            storeFile file(sourceStoreFile)
            storePassword sourcesStorePassword
        }
    }
    compileSdkVersion 26
    buildToolsVersion '26.0.1'

    int code = buildCode()

    defaultConfig {
        applicationId "com.sorcerer.sorcery.iconpack"
        versionCode code
        versionName "4.5." + code
        minSdkVersion 21
        targetSdkVersion 26
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        debug {
//            multiDexEnabled true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.SorceryKey
        }
        release {
            multiDexEnabled false
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.SorceryKey
        }
    }
    buildTypes.each {
        it.buildConfigField 'String', 'LEANCLOUD_ID', leancloudId
        it.buildConfigField 'String', 'LEANCLOUD_KEY', leancloudKey
        it.buildConfigField 'String', 'HE_WEATHER_KEY', heWeatherKey
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    // pack tool
    testCompile project(':packtools')

    compile 'com.android.support:appcompat-v7:26.0.1'
    compile 'com.android.support:design:26.0.1'
    compile 'com.android.support:support-v4:26.0.1'
    compile 'com.android.support:cardview-v7:26.0.1'
    compile 'com.android.support:recyclerview-v7:26.0.1'
    compile 'com.android.support:preference-v14:26.0.1'
    compile 'com.android.support:support-fragment:26.0.1'
    compile 'com.android.support:support-v13:26.0.1'

    // material dialog
    compile 'com.afollestad.material-dialogs:core:0.9.4.5'
    compile 'com.afollestad.material-dialogs:commons:0.9.4.5'

    // material drawer
    compile('com.mikepenz:materialdrawer:5.9.3@aar') {
        transitive = true
    }

    // indicator
    compile 'com.wang.avi:library:2.1.3'

    // xposed
    provided 'de.robv.android.xposed:api:82'
    provided 'de.robv.android.xposed:api:82:sources'

    // butterknife
    compile 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // glide
    compile 'com.github.bumptech.glide:glide:4.0.0-RC1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.0.0-RC1'

    // rx
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile "io.reactivex.rxjava2:rxjava:2.1.1"
    compile 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'
    compile 'com.github.VictorAlbertos:RxActivityResult:0.4.5-2.x'
    compile 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    compile 'com.f2prateek.rx.preferences2:rx-preferences:2.0.0-RC1'
    compile 'com.squareup.sqlbrite2:sqlbrite:2.0.0-SNAPSHOT'

    // timber
    compile 'com.jakewharton.timber:timber:4.5.1'

    // iconics
    compile 'com.mikepenz:iconics-core:2.8.7@aar'
    compile 'com.mikepenz:google-material-typeface:3.0.1.0.original@aar'
    compile 'com.mikepenz:fontawesome-typeface:4.7.0.0@aar'

    // slice
    compile 'com.github.mthli:Slice:v1.2'

    // gson
    compile 'com.google.code.gson:gson:2.8.1'

    // jsoup
    compile 'org.jsoup:jsoup:1.10.2'

    // stream on java7
    compile 'com.annimon:stream:1.1.7'

    // retrofit
    compile "com.squareup.retrofit2:retrofit:2.3.0"
    compile "com.squareup.retrofit2:converter-gson:2.3.0"
    compile "com.squareup.retrofit2:adapter-rxjava2:2.3.0"

    // autoValue
    provided 'com.google.auto.value:auto-value:1.3'
    annotationProcessor 'com.google.auto.value:auto-value:1.3'
    annotationProcessor 'com.ryanharter.auto.value:auto-value-parcel:0.2.5'

    // su
    compile 'eu.chainfire:libsuperuser:1.0.0.201704021214'

    // bottom sheet
    compile 'com.flipboard:bottomsheet-commons:1.5.3'

    // fastscroll
    compile 'com.github.turing-tech:MaterialScrollBar:12.3.1'

    // markdownView
    compile 'com.github.tiagohm.MarkdownView:library:0.16.0'
    // db debug
    debugCompile 'com.amitshekhar.android:debug-db:1.0.0'
    // swipe layout
    compile 'cn.bingoogolapple:bga-swipebacklayout:1.0.8@aar'
    // status bar
    compile 'com.jaeger.statusbarutil:library:1.4.0'
}
